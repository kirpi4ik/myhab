plugins {
    //https://github.com/node-gradle/gradle-node-plugin/releases
    id "com.github.node-gradle.node" version "7.1.0"
}
node {
    version = '22.13.0' // https://nodejs.org/en/
    yarnVersion = '1.22.22' // https://yarnpkg.com/en/
    download = true
}

task bootRun(dependsOn: 'serve') {
    group = 'application'
    description = 'Run the client app (for use with gradle bootRun -parallel'
}

task cleanNodeModules(type: Delete) {
    group = 'application'
    description = 'Clean node_modules and reinstall dependencies'
    delete "${projectDir}/node_modules"
    delete "${projectDir}/package-lock.json"
}

task yarnInstall(type: YarnTask) {
    group = 'application'
    description = 'Install yarn dependencies with force and check files'
    args = ['install', '--force', '--check-files']
}

task serve(type: YarnTask, dependsOn: ['yarnInstall']) {
    group = 'application'
    description = 'Run the client app'
    args = ['serve']
}

task build(type: YarnTask, dependsOn: 'yarn') {
    group = 'build'
    description = 'Build the client bundle'
    args = ['run', 'build']
}
task lint(type: YarnTask, dependsOn: 'yarn') {
    group = 'build'
    description = 'Run eslint'
    args = ['run', 'lint']
}
